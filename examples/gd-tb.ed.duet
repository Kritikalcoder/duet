-- gradient descent
-- total bound
let main = pλ m  : ℕ,
              n  : ℕ,
              ε  : ℝ⁺,
              k  : ℕ,
              δ  : ℝ⁺,
              η  : ℝ⁺
              .
              xs : 𝕄 [L∞ U|m,n] 𝔻 ,
              ys : 𝕄 [L∞ U|m,1] 𝔻 ,
              ε  : ℝ⁺[ε],
              k : ℕ[k],
              δ : ℝ⁺[δ],
              η : ℝ⁺[η]
              ⇒
  let εᵢ = ε / (ℝ⁺[2.0] ⋅ √ (ℝ⁺[2.0] ⋅ real k ⋅ (㏒ (ℝ⁺[2.0]/δ)))) in
  let δᵢ = δ / (ℝ⁺[2.0] ⋅ real k) in
  let m₀ = mcreate[ L∞ | ℕ[1] , cols xs ] { i , j ⇒ 0.0 } in
  aloop[ δ / ℝ⁺[2.0] ] k on m₀ <xs,ys> { a, θ ⇒
    let s = ℝ⁺[1.0] / real (rows xs) in
    g ← mgauss[ s , εᵢ , δᵢ ] <xs,ys> { ∇[ LR | θ ; mclip[L2] xs, ys ] } ;
    return mmap θ , mmap g { x ⇒ η ⋅ x } { x , y ⇒ x - y }
  }
in main
